from snakemake.utils import Paramspace
import subprocess
import pandas as pd

configfile: "config/config.yml"

params_csv = config['paramspace']
paramspace = Paramspace(pd.read_csv(params_csv), param_sep="-")
print(*[p for p in paramspace.instance_patterns], sep = '\n')

wildcards = glob_wildcards("data/raw_fastqs/{sample}.R{read}.fastq.gz")

rule all:
    input:
        expand("log/{params}/submitted.txt", params=paramspace.instance_patterns)

rule select_region:
    input:
        bam="data/bams/{sample}.chr22.split.bam",
    output:
        txt=temp("data/subset_fastqs/{sample}.chr22.split.subset.txt"),
    params:
        chrom=22,
        start=40797000,
        end=41000000,
    conda: "envs/samtools.yml"
    shell:
        """
        samtools view -b -h {input.bam} "chr{params.chrom}:{params.start}-{params.end}" | \
            samtools bam2fq -n - | \
            grep "^@" | \
            sed "s/^@//" | \
            sort | \
            uniq > {output.txt}
        """

rule subset_fastq:
    input:
        names=rules.select_region.output.txt,
        fastq="data/raw_fastqs/{sample}.R{read}.fastq.gz",
    output:
        fastq="data/subset_fastqs/{sample}.chr22.split.R{read}.fastq.gz",
    conda: "envs/seqtk.yml"
    shell:
        """
        seqtk subseq {input.fastq} {input.names} | gzip > {output.fastq}
        """

rule construct_xavier:
    input:
        fastq=expand('data/subset_fastqs/{sample}.chr22.split.R{read}.fastq.gz', 
                sample=wildcards.sample, read=(1,2))
    output:
        sh=f"results/{paramspace.wildcard_pattern}/command.sh"
    params:
        p=paramspace.instance,
        inputdir = 'data/subset_fastqs/*.fastq.gz',
        outdir=f'results/{paramspace.wildcard_pattern}',
        xavier = config['xavier_bin'],
        targets = config['targets'],
        pairs = config['pairs'],
        genome = config['genome'],
        runmodes = ("init", "dryrun", "run --mode slurm"),
    run:
        # required arguments
        options = ['--input', params.inputdir, '--output', params.outdir, '--targets', params.targets, '--genome', params.genome]
        # flags
        for flag in ('cnv', 'ffpe'):
            if params.p[flag] == 'y':
                options.append(f"--{flag}")
        # named arguments
        if params.p['pair'] == 'y':
            options += ['--pairs', params.pairs]
        
        with open(output.sh, 'w') as outfile:
            for runmode in params.runmodes:
                cmd=' '.join([params.xavier, "run", "--runmode", runmode] + options)
                print('COMMAND\n-------\n', cmd, '\n=======')
                
                if runmode.startswith('run'):
                    outfile.write(cmd)
                elif runmode == 'init':
                    # TODO: subprocess fails on 'run' mode, maybe b/c XAVIER itself uses subprocess?
                    subprocess.call(cmd, shell=True) 

rule submit_xavier:
    input:
        sh=rules.construct_xavier.output.sh
    log:
        txt=f"log/{paramspace.wildcard_pattern}/submitted.txt"
    shell:
        """
        bash {input.sh} > {log.txt}
        """
